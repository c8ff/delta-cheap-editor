plugins {
    id 'application'
    id 'idea'
}

var lwjglVersion = "3.3.2"
var lwjglNatives = "natives-windows"

group = 'dev.seeight'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

void downloadLibraries(String path, String... urls) {
    var folder = file(path)
    if (!folder.exists()) folder.mkdirs()

    for (final def url in urls) {
        def filePath = new File(folder, url.substring(url.lastIndexOf('/') + 1))

        var file = file(filePath)
        if (file.exists()) {
            continue
        }

        new URL(url).withInputStream { input -> file.withOutputStream { out -> out << input }}
    }
}

void dlRepo(String libName) {
    def folder = System.getenv("dumb_libraries") ?: "./lib"
    def repoLoc = "https://github.com/c8ff/${libName}/releases/download/latest"
    downloadLibraries("${folder}/${libName}/", "${repoLoc}/${libName}.jar", "${repoLoc}/${libName}-javadoc.jar", "${repoLoc}/${libName}-sources.jar")

    this.dependencies {
        implementation fileTree(dir: "${folder}/${libName}", include: ['*.jar'])
    }
}

dependencies {
	dlRepo("simple-renderer")
	dlRepo("common-lwjgl-stuff")
	dlRepo("various-utils")
	dlRepo("AstraKit")

    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    implementation "org.lwjgl:lwjgl-stb"
    implementation "org.lwjgl:lwjgl-nfd"
    runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-nfd::$lwjglNatives"

    implementation "org.joml:joml:1.10.5"
    implementation "commons-io:commons-io:2.13.0"
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "org.jetbrains:annotations:24.0.1"
    implementation 'org.apache.commons:commons-text:1.10.0'
}

application {
    mainClassName = 'Main'
}

idea {
    module {
        downloadJavadoc = true // defaults to false
        downloadSources = true
    }
}